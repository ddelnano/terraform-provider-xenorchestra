# xenorchestra_vm

Creates a Xen Orchestra vm resource.

## Example Usage

```hcl
data "xenorchestra_pool" "pool" {
    name_label = "pool name"
}

data "xenorchestra_template" "template" {
    name_label = "Puppet agent - Bionic 18.04 - 1"
}

data "xenorchestra_network" "net" {
  name_label = "Pool-wide network associated with eth0"
}

resource "xenorchestra_cloud_config" "bar" {
  name = "cloud config name"
  template = <<EOF
#cloud-config

runcmd:
 - [ ls, -l, / ]
 - [ sh, -xc, "echo $(date) ': hello world!'" ]
 - [ sh, -c, echo "=========hello world'=========" ]
 - ls -l /root
EOF
}

resource "xenorchestra_vm" "bar" {
    memory_max = 1073733632
    cpus  = 1
    cloud_config = xenorchestra_cloud_config.bar.template
    name_label = "Name"
    name_description = "description"
    template = data.xenorchestra_template.template.id

    # Prefer to run the VM on the primary pool instance
    affinity_host = data.xenorchestra_pool.pool.master
    network {
      network_id = data.xenorchestra_network.net.id
    }

    disk {
      sr_id = "7f469400-4a2b-5624-cf62-61e522e50ea1"
      name_label = "Ubuntu Bionic Beaver 18.04_imavo"
      size = 32212254720 
    }

    tags = [
      "Ubuntu",
      "Bionic",
    ]
}
```

## Argument Reference
* name_label - (Required) The name of VM.
* name_description - (Optional) The description of the VM.
* template - (Required) The ID of the VM template to create the new VM from.
* cloud_config - (Optional) The content of the cloud-init config to use
* cloud_network_config - (Optional) The content of the cloud-init network configuration for the VM (uses [version 1](https://cloudinit.readthedocs.io/en/latest/topics/network-config-format-v1.html))
* cpus - (Required) The number of CPUs the VM will have.
* memory_max - (Required) The amount of memory in bytes the VM will have.
* high_availabililty - (Optional) The restart priority for the VM. Possible values are `best-effort`, `restart` and empty string (no restarts on failure. Defaults to empty string.
* auto_poweron - (Optional) If the VM will automatically turn on. Defaults to `false`.
* affinity_host - (Optional) The preferred host you would like the VM to run on. If changed on an existing VM it will require a reboot for the VM to be rescheduled.
* network - (Required) The network the VM will use
    * network_id - (Required) The ID of the network the VM will be on.
    * mac_address - (Optional) The mac address of the network interface. This must be parsable by go's [net.ParseMAC function](https://golang.org/pkg/net/#ParseMAC). All mac addresses are stored in Terraform's state with [HardwareAddr's string representation](https://golang.org/pkg/net/#HardwareAddr.String) i.e. 00:00:5e:00:53:01
* disk - (Required) The disk the VM will have access to.
    * sr_id - (Required) The storage repository ID to use.
    * name_label - (Required) The name for the disk.
    * name_description - (Optional) A description for the disk.
    * size - (Required) The size in bytes of the disk.
* tags - (Optional) List of labels (strings) that are used to identify and organize resources. These are equivalent to Xenserver [tags](https://docs.citrix.com/en-us/xencenter/7-1/resources-tagging.html).

## Attributes Reference
* id is set to the ID generated by the XO api.
